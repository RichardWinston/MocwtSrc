C  CRIV5   SOLUTE SINK/SOURCES AT RIVERS
C
C*************************************************************************
C
      SUBROUTINE GWT1RIV5BD(NRIVER,MXRIVR,
     *  RIVR,IBOUND,NCOL,NROW,NLAY,KSTP,KPER,IPERGWT,
     *  SRCFLO,SRCSOL,SNKFLO,
     *  NSCOL,NSROW,NSLAY,IOUTS,NRIVVL,IRIVAL,IRIVRC,ICONLY,NOPRRV,
     *  VC,VR,VL,IFACEC)
C
C*************************************************************************
C
      DIMENSION VC(NSCOL+1,NSROW,NSLAY),VR(NSCOL,NSROW+1,NSLAY),
     *   VL(NSCOL,NSROW,NSLAY+1)  
      CHARACTER*8 PACKAGE
C
      DIMENSION RIVR(NRIVVL,MXRIVR)
      DIMENSION IBOUND(NCOL,NROW,NLAY),
     *  SRCFLO(NSCOL,NSROW,NSLAY),SRCSOL(NSCOL,NSROW,NSLAY),
     *  SNKFLO(NSCOL,NSROW,NSLAY)
      COMMON /RIVCOM/RIVAUX(5)
      CHARACTER*16 RIVAUX
C
      COMMON /GWT/ CDEL,RDEL,CNOFLO,CELDIS,FZERO,NZCRIT
      COMMON /SUBGRD/
     *  ISCOL1,ISCOL2,ISROW1,ISROW2,ISLAY1,ISLAY2,ISUBGD
C
C*************************************************************************
CRIV5FM  FIND OUT WHICH OF THE RIVER AUXILIARY PARAMETERS IS CONC
C  IF NOT FOUND, AND RIVERS ARE WITHIN SUBGRID, THEN STOP
C  CHECK EXISTENCE OF CBCALLOCATE OPTION
      IF (IRIVAL.NE.1) THEN
         WRITE(IOUTS,*) ' ERROR, CBCALLOCATE OPTION MUST BE ',
     *        'SPECIFIED IN RIVER INPUT DATA'
         STOP
      ENDIF
C
C RETURN IF NOT SOLVING TRANSPORT YET
      IF(KPER.LT.IPERGWT) RETURN
C
C  SET POINTER TO CONCENTRATION DATA FOR RIVERS
C  SET POINTER TO IFACE VALUE FOR RIVERS
      IF(KPER.EQ.IPERGWT.AND.KSTP.EQ.1) THEN
C COUNT FROM THE TOTAL NUMBER OF STANDARD DATA INPUTS (6)
C INITIALIZE IFACEC
         IFACEC=0
         NAUX=NRIVVL-6-IRIVAL
         IRIVRC=0
         IF(NAUX.GT.0) THEN
            DO 8 IAUX=1,NAUX
               IF(RIVAUX(IAUX).EQ.'CONCENTRATION'.OR.
     *            RIVAUX(IAUX).EQ.'CONC') THEN
                  IRIVRC=IAUX+6
               END IF
               IF(RIVAUX(IAUX).EQ.'IFACE') THEN
                  IFACEC=IAUX+6
               END IF
 8          CONTINUE
         END IF
         IF(IRIVRC.EQ.0) WRITE(IOUTS,*) ' WARNING, ',
     *         'NO CONCENTRATION DATA READ FOR RIVER NODES'
         IF(IFACEC.EQ.0) WRITE(IOUTS,*) ' IFACE NOT DEFINED: ALL RIVERS' 
     *         ,' APPLIED AS DISTRIBUTED SOURCES/SINKS'
      END IF
C
C  RETURN IF NO ACTIVE RIVER NODES THIS PERIOD
      IF(NRIVER.LE.0) RETURN
C
C5------PRINT RIVER CONCENTRATION AND FORMULATE
C        SINK AND SOURCE TERMS FOR TRANSPORT
      IRIVSG=0
      IERSRC=0
      ITHKSG=0
      IPRNT=0
      DO 30 IR=1,NRIVER
         K=RIVR(1,IR)
         KS=K-ISLAY1+1
         IF(KS.LT.1.OR.KS.GT.NSLAY) GO TO 30
         I=RIVR(2,IR)
         IS=I-ISROW1+1
         IF(IS.LT.1.OR.IS.GT.NSROW) GO TO 30
         J=RIVR(3,IR)
C  SKIP RIVERS IN FIXED HEAD CELLS
         IF(IBOUND(J,I,K).LE.0) THEN
           ITHKSG=ITHKSG+1
           GO TO 30
         ENDIF
         JS=J-ISCOL1+1
         IF(JS.LT.1.OR.JS.GT.NSCOL) GO TO 30
         IF(KSTP.EQ.1) THEN
           IF(IFACEC.LE.0) THEN
             IF(IRIVRC.LE.0) THEN
              IF(IPRNT.EQ.0.AND.NOPRRV.NE.1) WRITE(IOUTS,29)
   29         FORMAT(//3X,'RIVER CONCENTRATIONS FOR THIS STRESS PERIOD'/
     2      1X,'  RIVER SEG NO.  LAYER      ROW     COLUMN ',
     3      /1X,55('-'))
              IPRNT=1
              IF(NOPRRV.NE.1) WRITE(IOUTS,'(1X,4I10)') IR,K,I,J
             ELSE
              IF(IPRNT.EQ.0.AND.NOPRRV.NE.1) WRITE(IOUTS,129)
  129         FORMAT(//3X,'RIVER CONCENTRATIONS FOR THIS STRESS PERIOD'/
     2      1X,'  RIVER SEG NO.  LAYER      ROW     COLUMN ',
     3      ' SOURCE CONC'/1X,55('-'))
              IPRNT=1
              IF(NOPRRV.NE.1) WRITE(IOUTS,'(1X,4I10,2X,1PE12.4)') 
     *           IR,K,I,J,RIVR(IRIVRC,IR)
             END IF
           ELSE
             IFACE=RIVR(IFACEC,IR)
             IF(IRIVRC.LE.0) THEN
              IF(IPRNT.EQ.0.AND.NOPRRV.NE.1) WRITE(IOUTS,229)
  229         FORMAT(//3X,'RIVER CONCENTRATIONS FOR THIS STRESS PERIOD'/
     2      1X,'  RIVER SEG NO.  LAYER      ROW     COLUMN     IFACE',
     3      /1X,64('-'))
              IPRNT=1
              IF(NOPRRV.NE.1) WRITE(IOUTS,'(1X,5I10)') IR,K,I,J,IFACE
             ELSE
              IF(IPRNT.EQ.0.AND.NOPRRV.NE.1) WRITE(IOUTS,329)
  329         FORMAT(//3X,'RIVER CONCENTRATIONS FOR THIS STRESS PERIOD'/
     2      1X,'  RIVER SEG NO.  LAYER      ROW     COLUMN ',
     3      ' SOURCE CONC    IFACE'/1X,64('-'))
              IPRNT=1
              IF(NOPRRV.NE.1) WRITE(IOUTS,'(1X,4I10,2X,1PE12.4,I10)') 
     *           IR,K,I,J,RIVR(IRIVRC,IR),IFACE
             END IF
           END IF
         END IF
C
         IF(ICONLY.EQ.1) THEN
            IRIVSG=IRIVSG+1
         ELSE
            RATE=RIVR(NRIVVL,IR)
            IFACE=0
            IF(IFACEC.GT.0) IFACE=RIVR(IFACEC,IR)
            IF(IFACE.LT.0) IFACE=-1
            IF(IFACE.NE.0) THEN
              PACKAGE='   RIVER'
              IPCK=IR
              CALL GWT1BFLX5PCK(IFACE,IBOUND,RATE,PACKAGE,IPCK,
     *         VC,VR,VL,
     *         NCOL,NROW,NLAY,NSCOL,NSROW,NSLAY,
     *         IOUTS,
     *         J,I,K,JS,IS,KS)
            END IF
C
            IF(RATE.LT.0.0) THEN
               SNKFLO(JS,IS,KS)=SNKFLO(JS,IS,KS)+RATE
            ELSE IF(RATE.GT.0.0) THEN
               IF(IRIVRC.LE.0) THEN
                  IERSRC=IERSRC+1
               ELSE
                  SRCFLO(JS,IS,KS)=SRCFLO(JS,IS,KS)+RATE
                  RATEC=RATE*RIVR(IRIVRC,IR)
                  SRCSOL(JS,IS,KS)=SRCSOL(JS,IS,KS)+RATEC
               END IF
            END IF
         END IF
   30 CONTINUE
C
C  STOP IF RIVER REACH WITHIN TRANSPORT SUBGRID AND ICONLY=1
      IF(IRIVSG.GT.0) THEN
         WRITE(IOUTS,*) ' ERROR,',IRIVSG,' RIVER NODES WITHIN',
     *      ' SUBGRID, BUT ICONLY=1, STOPPING'
         STOP
      END IF
C  STOP IF SOURCE RIVER NODE WITHIN SUBGRID, BUT NO CONC DATA
      IF(IERSRC.GT.0) THEN
         WRITE(IOUTS,*) ' ERROR,',IERSRC,' SOURCE RIVER NODES',
     *     ' WITHIN SUBGRID, BUT NO CONCENTRATION DATA, STOPPING'
         STOP
      END IF
C  WARN IF WELL NODE WITHIN SUBGRID, BUT NO FLOW ASSOC WITH IT
      IF(ITHKSG.GT.0) THEN
         WRITE(IOUTS,*) ' WARNING:',ITHKSG,' RIVER NODES',
     *     ' SKIPPED WITHIN SUBGRID (IBOUND <= 0)'
      ENDIF
C
      RETURN
      END
