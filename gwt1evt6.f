C  CEVT5 EVAPOTRANSPORATION SOLUTE SINK
C     ***************************************************************
C
      SUBROUTINE GWT1EVT6BD(NEVTOP,BUFF,EVTFLO,IEVT,
     *  IBOUND,
     *  NCOL,NROW,NLAY,
     *  NSCOL,NSROW,NSLAY,IOUTS,ICONLY,
     *  VL,IEVTTP)
C
C     ***************************************************************
C
      DIMENSION
     *  IEVT(NCOL,NROW),BUFF(NCOL,NROW,NLAY),
     *  IBOUND(NCOL,NROW,NLAY), 
cgzh EVTFLO NOW HAS TWO PARTS: 1) FLUX, 2) LAYER OF ET
     *  EVTFLO(NSCOL,NSROW,2),
     *  VL(NSCOL,NSROW,NSLAY+1)
      COMMON /GWT/ CDEL,RDEL,CNOFLO,CELDIS,FZERO,NZCRIT
C
      COMMON /SUBGRD/
     *  ISCOL1,ISCOL2,ISROW1,ISROW2,ISLAY1,ISLAY2,ISUBGD
C
C     ***************************************************************
C
C  SKIP IF ICONLY=1
      IF(ICONLY.EQ.1) RETURN
C
C  SKIP IF NEVTOP=1 AND NO TRANSPORT IN TOP LAYER
      IF(NEVTOP.NE.2.AND.ISLAY1.NE.1) RETURN
C
C  EVAPOTRANSPIRATION
      ITHKSG=0
      DO 10 IS=1,NSROW
      I=IS+ISROW1-1
      DO 10 JS=1,NSCOL
      J=JS+ISCOL1-1
C
C4------SET THE LAYER INDEX -- FOR OPTION 1, THE LAYER IS 1;
C4------FOR OPTION 2, THE LAYER IS SPECIFIED IN IEVT.
      IF(NEVTOP.EQ.1) THEN
         K=1
      ELSE IF(NEVTOP.EQ.2) THEN
         K=IEVT(J,I)
      ELSE
C
C5------FOR OPTION 3, FIND UPPERMOST ACTIVE CELL.
         DO 3 K=1,NLAY
         IF(IBOUND(J,I,K).NE.0) GO TO 4
    3    CONTINUE
         K=1
    4    IEVT(J,I)=K
      END IF
      IF(IBOUND(J,I,K).LE.0) THEN
        ITHKSG=ITHKSG+1
        GO TO 10
      ENDIF
      KS=K-ISLAY1+1
      IF(KS.LT.1.OR.KS.GT.NSLAY) GO TO 10
         EVTFLO(JS,IS,1)=BUFF(J,I,K)
         EVTFLO(JS,IS,2)=K
         IF(IEVTTP.GT.0) VL(JS,IS,KS)=VL(JS,IS,KS)+BUFF(J,I,K)
c
   10 CONTINUE
C  WARN IF ET NODE WITHIN SUBGRID, BUT NO FLOW ASSOC WITH IT
      IF(ITHKSG.GT.0) THEN
         WRITE(IOUTS,*) ' WARNING:',ITHKSG,' EVT NODES',
     *     ' SKIPPED WITHIN SUBGRID (IBOUND <= 0)'
      END IF
C
      RETURN
      END
