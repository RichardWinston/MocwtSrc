C  CGHB5  SOLUTE SINK/SOURCES AT GENERAL FIXED HEAD BOUNDARIES
C
C*************************************************************************
C
      SUBROUTINE GWT1GHB5BD(NBOUND,MXBND,
     *  BNDS,IBOUND,NCOL,NROW,NLAY,KSTP,KPER,IPERGWT,
     *  SRCFLO,SRCSOL,SNKFLO,
     *  NSCOL,NSROW,NSLAY,IOUTS,NGHBVL,IGHBAL,IBNDSC,ICONLY,NOPRGB,
     *  VC,VR,VL,IFACEC)
C
C     ***************************************************************
C
      DIMENSION VC(NSCOL+1,NSROW,NSLAY),VR(NSCOL,NSROW+1,NSLAY),
     *   VL(NSCOL,NSROW,NSLAY+1)  
      CHARACTER*8 PACKAGE
C
      DIMENSION BNDS(NGHBVL,MXBND)
      DIMENSION IBOUND(NCOL,NROW,NLAY),
     *  SRCFLO(NSCOL,NSROW,NSLAY),SRCSOL(NSCOL,NSROW,NSLAY),
     *  SNKFLO(NSCOL,NSROW,NSLAY)
      COMMON /GHBCOM/GHBAUX(5)
      CHARACTER*16 GHBAUX
C
      COMMON /GWT/ CDEL,RDEL,CNOFLO,CELDIS,FZERO,NZCRIT
      COMMON /SUBGRD/
     *  ISCOL1,ISCOL2,ISROW1,ISROW2,ISLAY1,ISLAY2,ISUBGD
C
C     ***************************************************************
C  CGHB5FM  FIND OUT WHICH OF THE GEN. HEAD AUXILIARY PARAMETERS IS CONC
C  IF NOT FOUND, AND GEN. HEAD NODES ARE WITHIN SUBGRID, THEN STOP
C
C  CHECK EXISTENCE OF CBCALLOCATE OPTION
      IF (IGHBAL.NE.1) THEN
         WRITE(IOUTS,*) ' ERROR, CBCALLOCATE OPTION MUST BE ',
     *        'SPECIFIED IN GENERAL HEAD INPUT DATA'
         STOP
      ENDIF
C RETURN IF NOT SOLVING TRANSPORT YET
      IF(KPER.LT.IPERGWT) RETURN
C
C  SET POINTER TO CONCENTRATION DATA FOR GENERAL HEAD NODES
C  SET POINTER TO IFACE VALUE FOR GHBS
      IF(KPER.EQ.IPERGWT.AND.KSTP.EQ.1) THEN
C COUNT FROM THE TOTAL NUMBER OF STANDARD DATA INPUTS (5)
         NAUX=NGHBVL-5-IGHBAL
         IBNDSC=0
         IF(NAUX.GT.0) THEN
            DO 8 IAUX=1,NAUX
               IF(GHBAUX(IAUX).EQ.'CONCENTRATION'.OR.
     *            GHBAUX(IAUX).EQ.'CONC') THEN
                  IBNDSC=IAUX+5
               END IF
               IF(GHBAUX(IAUX).EQ.'IFACE') THEN
                  IFACEC=IAUX+5
               END IF
 8          CONTINUE
         END IF
         IF(IBNDSC.EQ.0) WRITE(IOUTS,*) ' WARNING, NO CONCENTRATION ',
     *         'DATA READ FOR GENERAL HEAD NODES'
         IF(IFACEC.EQ.0) WRITE(IOUTS,*) ' IFACE NOT DEFINED: ALL WELLS', 
     *         ' APPLIED AS DISTRIBUTED SOURCES/SINKS'
      END IF
C
C  RETURN IF NO ACTIVE GENERAL HEAD NODES THIS PERIOD
      IF(NBOUND.LE.0) RETURN
C
C1------PRINT GENERAL HEAD CONCENTRATION AND FORMULATE
C        SINK AND SOURCE TERMS FOR TRANSPORT
C
      IGHBSG=0
      IERSRC=0
      ITHKSG=0
      IPRNT=0
      DO 10 IB=1,NBOUND
         K=BNDS(1,IB)
         KS=K-ISLAY1+1
         IF(KS.LT.1.OR.KS.GT.NSLAY) GO TO 10
         I=BNDS(2,IB)
         IS=I-ISROW1+1
         IF(IS.LT.1.OR.IS.GT.NSROW) GO TO 10
         J=BNDS(3,IB)
C  SKIP GENERAL HEAD BOUNDARIES AT FIXED HEAD NODES
         IF(IBOUND(J,I,K).LE.0) THEN
           ITHKSG=ITHKSG+1
           GO TO 10
         ENDIF
         JS=J-ISCOL1+1
         IF(JS.LT.1.OR.JS.GT.NSCOL) GO TO 10
         IF(KSTP.EQ.1) THEN
           IF(IFACEC.LE.0) THEN
             IF(IBNDSC.LE.0) THEN
              IF(IPRNT.EQ.0.AND.NOPRGB.NE.1) WRITE(IOUTS,29)
   29         FORMAT(//1X,'GHB CONCENTRATIONS FOR THIS STRESS PERIOD'/
     1   '  ONLY FOR GHB NODES WITHIN TRANSPORT SUBGRID'//
     2   5X,'  GHB NO.    LAYER      ROW     COLUMN   '/
     3     1X,56('-'))
              IPRNT=1
              IF(NOPRGB.NE.1) WRITE(IOUTS,'(1X,4I10)') IB,K,I,J
             ELSE
              IF(IPRNT.EQ.0.AND.NOPRGB.NE.1) WRITE(IOUTS,129)
  129         FORMAT(//1X,'GHB CONCENTRATIONS FOR THIS STRESS PERIOD'/
     1   '  ONLY FOR GHB NODES WITHIN TRANSPORT SUBGRID'//
     2   5X,'  GHB NO.    LAYER      ROW     COLUMN   SOURCE CONC'/
     3     1X,56('-'))
              IPRNT=1
              IF(NOPRGB.NE.1) WRITE(IOUTS,'(1X,4I10,3X,1PE12.4)') 
     *           IB,K,I,J,BNDS(IBNDSC,IB)
             END IF
           ELSE
             IFACE=BNDS(IFACEC,IB)
             IF(IBNDSC.LE.0) THEN
              IF(IPRNT.EQ.0.AND.NOPRGB.NE.1) WRITE(IOUTS,229)
  229         FORMAT(//1X,'GHB CONCENTRATIONS FOR THIS STRESS PERIOD'/
     1   '  ONLY FOR GHB NODES WITHIN TRANSPORT SUBGRID'//
     2   5X,'  GHB NO.    LAYER      ROW     COLUMN     IFACE'/
     3     1X,65('-'))
              IPRNT=1
              IF(NOPRGB.NE.1) WRITE(IOUTS,'(1X,5I10)') IB,K,I,J,IFACE
             ELSE
              IF(IPRNT.EQ.0.AND.NOPRGB.NE.1) WRITE(IOUTS,329)
  329         FORMAT(//1X,'GHB CONCENTRATIONS FOR THIS STRESS PERIOD'/
     1   '  ONLY FOR GHB NODES WITHIN TRANSPORT SUBGRID'//
     2   5X,'  GHB NO.    LAYER      ROW     COLUMN   SOURCE CONC    
     3IFACE'/
     4     1X,65('-'))
              IPRNT=1
              IF(NOPRGB.NE.1) WRITE(IOUTS,'(1X,4I10,3X,1PE12.4,I10)') 
     *           IB,K,I,J,BNDS(IBNDSC,IB),IFACE
             END IF
            END IF
         END IF
C
         IF(ICONLY.EQ.1) THEN
            IGHBSG=IGHBSG+1
         ELSE
            RATE=BNDS(NGHBVL,IB)
            IFACE=0
            IF(IFACEC.GT.0) IFACE=BNDS(IFACEC,IB)
            IF(IFACE.LT.0) IFACE=-1
            IF(IFACE.NE.0) THEN
              PACKAGE='     GHB'
              IPCK=IB
              CALL GWT1BFLX5PCK(IFACE,IBOUND,RATE,PACKAGE,IPCK,
     *         VC,VR,VL,
     *         NCOL,NROW,NLAY,NSCOL,NSROW,NSLAY,
     *         IOUTS,
     *         J,I,K,JS,IS,KS)
            END IF
C
            IF(RATE.LT.0.0) THEN
               SNKFLO(JS,IS,KS)=SNKFLO(JS,IS,KS)+RATE
            ELSE IF(RATE.GT.0.0) THEN
               IF(IBNDSC.LE.0) THEN
                  IERSRC=IERSRC+1
               ELSE
                  SRCFLO(JS,IS,KS)=SRCFLO(JS,IS,KS)+RATE
                  RATEC=RATE*BNDS(IBNDSC,IB)
                  SRCSOL(JS,IS,KS)=SRCSOL(JS,IS,KS)+RATEC
               END IF
            END IF
         END IF
   10 CONTINUE
C
C  STOP IF GEN. HEAD NODE WITHIN TRANSPORT SUBGRID AND ICONLY=1
      IF(IGHBSG.GT.0) THEN
         WRITE(IOUTS,*) ' ERROR,',IGHBSG,' GEN. HEAD NODES WITHIN',
     *      ' SUBGRID, BUT ICONLY=1, STOPPING'
         STOP
      END IF
C  STOP IF SOURCE GEN. HEAD NODE WITHIN SUBGRID, BUT NO CONC DATA
      IF(IERSRC.GT.0) THEN
         WRITE(IOUTS,*) ' ERROR:',IERSRC,' SOURCE GEN. HEAD NODES',
     *     ' WITHIN SUBGRID, BUT NO CONCENTRATION DATA, STOPPING'
         STOP
      END IF
C  WARN IF WELL NODE WITHIN SUBGRID, BUT NO FLOW ASSOC WITH IT
      IF(ITHKSG.GT.0) THEN
         WRITE(IOUTS,*) ' WARNING:',ITHKSG,' GHB NODES',
     *     ' SKIPPED WITHIN SUBGRID (IBOUND <= 0)'
      ENDIF
C
      RETURN
      END
