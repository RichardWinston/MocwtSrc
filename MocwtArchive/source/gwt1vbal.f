      MODULE VBALMOD
        INTEGER VBAL
        ALLOCATABLE VBAL(:,:,:)
      END MODULE VBALMOD
      
      SUBROUTINE GWT1VBAL1AL(IOUTS, NSCOL,NSROW,NSLAY)
! IOUTS: UNIT NUMBER FOR THE MAIN GWT OUTPUT FILE
! INVBAL: UNIT NUMBER FOR VBAL INPUT FILE = JUNIT(28)  
      USE VBALMOD 
      INCLUDE 'ptwt.inc'
      COMMON /SUBGRD/
     *  ISCOL1,ISCOL2,ISROW1,ISROW2,ISLAY1,ISLAY2,ISUBGD
      WRITE(IOUTS,*)
      WRITE(IOUTS,*) 'VBAL PACKAGE'
      IF (PTWTON.EQ.0) THEN
        WRITE(IOUTS,*) '***WARNING*** VBAL PACKAGE IS NOT BEING READ  ',
     *    'BECAUSE IT IS ONLY USED WITH THE MOCWT OR MOCWTI OPTIONS.'
        RETURN
      ENDIF
      ALLOCATE (VBAL(NSCOL,NSROW,NSLAY))
      VBAL = 0
10000 RETURN
      END;
      
      SUBROUTINE GWT1VBAL1RP(IOUTS, INVBAL)
      USE VBALMOD 
      INCLUDE 'ptwt.inc'
      COMMON /SUBGRD/
     *  ISCOL1,ISCOL2,ISROW1,ISROW2,ISLAY1,ISLAY2,ISUBGD
      CHARACTER*200 LINE
      INTEGER NBAL, LAYER, ROW, COLUMN
      LOGICAL ERROR
      IF (PTWTON.EQ.0) THEN
        RETURN
      ENDIF
c     VBAL IS USED IN SUBROUTINE PTWT1UP       
     
      CALL URDCOM(INVBAL,IOUTS,LINE)
      LLOC=1
      ERROR = .FALSE.
      CALL URWORD(LINE,LLOC,ISTART,ISTOP,2,NBAL,R,IOUTS,INVBAL)
      WRITE (IOUTS, *) 'Volume balancing will be applied to any weak'
      WRITE (IOUTS, *) 'sources at the following locations'
      WRITE (IOUTS, *) 
      WRITE (IOUTS, 100) 'VBAL NO.  LAYER   ROW   COL' 
      WRITE (IOUTS, 100) '-------- ------ ----- -----' 
100   FORMAT(1X,A)         
      DO IVBAL=1, NBAL
        READ(INVBAL,'(A)') LINE
        LLOC=1
        CALL URWORD(LINE,LLOC,ISTART,ISTOP,2,LAYER,R,IOUTS,INVBAL)
        CALL URWORD(LINE,LLOC,ISTART,ISTOP,2,ROW,R,IOUTS,INVBAL)
        CALL URWORD(LINE,LLOC,ISTART,ISTOP,2,COLUMN,R,IOUTS,INVBAL)
        WRITE (IOUTS, 200) IVBAL,LAYER,ROW,COLUMN 
200     FORMAT (1X,I8,1X,I6,1X,I5,1X,I5)       
300     FORMAT (1X,'IN THE VBAL FILE, THE ',A6,' OF ITEM ',I7,' (',I5,
     *    ') IS OUTSIDE THE TRANSPORT SUBGRID.')
        IF ((LAYER.LT.ISLAY1).OR.(LAYER.GT.ISLAY2)) THEN
          WRITE(IOUTS,300) ' LAYER', IVBAL, LAYER
          ERROR = .TRUE.
        ELSE
          LAYER = LAYER - ISLAY1 + 1
        ENDIF
        IF ((ROW.LT.ISROW1).OR.(ROW.GT.ISROW2)) THEN
          WRITE(IOUTS,300) '   ROW', IVBAL, ROW
          ERROR = .TRUE.
        ELSE
          ROW = ROW - ISROW1 + 1
        ENDIF
        IF ((COLUMN.LT.ISCOL1).OR.(COLUMN.GT.ISCOL2)) THEN
          WRITE(IOUTS,300) 'COLUMN', IVBAL, COLUMN
          ERROR = .TRUE.
        ELSE
          COLUMN = COLUMN - ISCOL1 + 1
        ENDIF
        IF (ERROR) CALL USTOP('ERROR READING VBAL INPUT FILE')
        VBAL(COLUMN,ROW,LAYER) = 1
      END DO
      
10000 RETURN
      END;
      
      SUBROUTINE GWT1VBAL1DA()
      USE VBALMOD 
      IF (ALLOCATED(VBAL)) DEALLOCATE(VBAL)
10000 RETURN
      END;
      