C
C GWT1CCBD1AL ALLOCATE SPACE FOR CONSTANT-CONCENTRATION BOUNDARIES
C
C     ******************************************************************
C
      SUBROUTINE GWT1CCBD1AL(ISUMRX,LSCCBD,INCCBD,IOUTS,PTWTON,
     *        NSCOL,NSROW,NSLAY)
C
C     ******************************************************************
C
      INTEGER PTWTON
C CCBD ONLY AVAILABLE FOR WEIGHTED VERSIONS
      IF(INCCBD.GT.0) THEN 
        IF(PTWTON.EQ.0) THEN
	    WRITE(IOUTS,*) '***ERROR*** CCBD PACKAGE ONLY COMPATIBLE WITH
     & WEIGHT PARTICLES OPTION ON'
          STOP 'CCBD ERROR'
        END IF
C IF CCBD IS OFF, SET POINTER AND RETURN
	ELSE
        LSCCBD=1
	  RETURN
	END IF 
C IF CCBD IS ON AND PTWTON IS TRUE, ALLOCATE SPACE IN RX
      ISP=NSCOL*NSROW*NSLAY
      LSCCBD=ISUMRX
      ISUMRX=ISUMRX+ISP
      WRITE(IOUTS,101) ISP
  101 FORMAT(1X,I8,' ELEMENTS IN RX ARRAY ARE USED BY CCBD')
C
      RETURN
      END
C
C
C  GWT1CCBD1RPS READ CONSTANT-CONCENTRATION BOUNDARIES
C
C     ******************************************************************
C
      SUBROUTINE GWT1CCBD1RPS(INCCBD,CCBDY,CONC,IOUTS,
     *     NCOL,NROW,NLAY,NSCOL,NSROW,NSLAY,KKPER,IPERGWT,
     *     DPCON,DPZO,INDP,
     *     DKZO,DKZS,INDK)
C
C     ******************************************************************
C
C     READ CONSTANT-CONCENTRATION BOUNDARIES
C     ******************************************************************
C
      DIMENSION CCBDY(NSCOL,NSROW,NSLAY),CONC(NSCOL,NSROW,NSLAY)              
      DIMENSION DPCON(NSCOL,NSROW,NSLAY),DPZO(NSCOL,NSROW,NSLAY)
      DIMENSION DKZO(NSCOL,NSROW,NSLAY),DKZS(NSCOL,NSROW,NSLAY)
C
      COMMON /SUBGRD/
     *  ISCOL1,ISCOL2,ISROW1,ISROW2,ISLAY1,ISLAY2,ISUBGD
C
C     ******************************************************************
C
C READ NUMBER OF CONSTANT-CONCENTRATION BOUNDARIES
      READ(INCCBD,*) ITMP
      IF(ITMP.LT.0) THEN
C IF KKPER=1, INVALID INPUT, RESET ITMP=0 (meaning no CCBDs)
        IF(KKPER.EQ.IPERGWT) THEN
           ITMP=0
         WRITE(IOUTS,5)
    5    FORMAT(1X,/
     1    1X,'***WARNING*** ITMP<0 NOT VALID FOR FIRST STRESS PERIOD; 
     2RESET ITMP=0 (NO CCBD CELLS THIS SP)')
         RETURN
        END IF
C IF < 0, REUSE VALUES FROM LAST SP
         WRITE(IOUTS,6)
    6    FORMAT(1X,/
     1    1X,'REUSING CONSTANT-CONCENTRATION BOUNDARIES
     2 FROM LAST STRESS PERIOD')
         RETURN
      ELSE
C INITIALIZE (DEFAULT IS -1.0, THIS SIGNIFIES NO BOUNDARY CONDITION HERE)
         NCCBDY=ITMP
         CCBDY=-1.0
      END IF
C WRITE HEADER TO OUTPUT FILE
      WRITE(IOUTS,'(///,A)') 'CONSTANT-CONCENTRATION BOUNDARY 
     &PACKAGE ACTIVE'
      IF(NCCBDY.EQ.1) THEN
	  WRITE (IOUTS,140) NCCBDY
      ELSE
	  WRITE (IOUTS,141) NCCBDY
	END IF
      WRITE (IOUTS,150)
C READ AND FILL CCBDY
      DO 30 N=1,NCCBDY
        READ(INCCBD,*) K,I,J,CCCONC
C CHECK FOR VALID SUBGRID CELL
        IF(K.LT.ISLAY1.OR.K.GT.ISLAY2.OR. 
     *      I.LT.ISROW1.OR.I.GT.ISROW2.OR. 
     *      J.LT.ISCOL1.OR.J.GT.ISCOL2) THEN
            WRITE(IOUTS,*) '***ERROR***   
     * CONSTANT-CONCENTRATION BOUNDARY CELL OUTSIDE SUBGRID'
       WRITE(IOUTS,'(3I8,5X,F14.6)') K,I,J,CCCONC            
            STOP 'CCBD ERROR'
        END IF       
        JS=J-ISCOL1+1
        IS=I-ISROW1+1
        KS=K-ISLAY1+1
        WRITE(IOUTS,'(3I8,5X,F14.6)') KS,IS,JS,CCCONC            
C CHECK AGAINST INITIAL CONCENTRATION
        IF(CONC(JS,IS,KS).NE.CCCONC) THEN
	    WRITE(IOUTS,*) '***WARNING*** CCBD VALUE ABOVE OVERRIDES
     1 INITIAL CONCENTRATION OF ',CONC(JS,IS,KS)
          CONC(JS,IS,KS)=CCCONC
	  END IF 
C CHECK AGAINST DOUBLE POROSITY CONCENTRATION
        IF(INDP.GT.0) THEN
	   IF(DPCON(JS,IS,KS).NE.CCCONC) THEN
	    WRITE(IOUTS,*) '***WARNING*** CCBD VALUE ABOVE OVERRIDES
     1 INIT DP CONCENTRATION OF',DPCON(JS,IS,KS)
          DPCON(JS,IS,KS)=CCCONC
	   END IF 
C CHECK AGAINST DOUBLE POROSITY ZERO-ORDER GROWTH
	   IF(DPZO(JS,IS,KS).NE.0.0) THEN
	    WRITE(IOUTS,*) '***WARNING*** CCBD NOT COMPATIBLE WITH
     1 DP ZERO-ORDER GROWTH, RESET TO 0.0'
          DPZO(JS,IS,KS)=0.0
	   END IF 
	  END IF 
C CHECK AGAINST SIMPLE REATION ZERO-ORDER GROWTH
        IF(INDK.GT.0) THEN
	   IF(DKZO(JS,IS,KS).NE.0.0) THEN
	    WRITE(IOUTS,*) '***WARNING*** CCBD NOT COMPATIBLE WITH
     1 DK DISSOLVED ZERO-ORDER GROWTH, RESET TO 0.0'
          DPZO(JS,IS,KS)=0.0
	   END IF 
	   IF(DKZS(JS,IS,KS).NE.0.0) THEN
	    WRITE(IOUTS,*) '***WARNING*** CCBD NOT COMPATIBLE WITH
     1 DK SORBED ZERO-ORDER GROWTH, RESET TO 0.0'
          DPZO(JS,IS,KS)=0.0
	   END IF 
	  END IF 
C SAVE VALUE IN CCBDY FOR USE WHEN APPLYING CCBD CELLS
        CCBDY(JS,IS,KS)=CCCONC
C
   30 CONTINUE
      WRITE(IOUTS,*)
      WRITE(IOUTS,*)
C
  140 FORMAT('COORDINATES FOR',I4,
     * ' CONSTANT-CONCENTRATION BOUNDARY CELL')
  141 FORMAT('COORDINATES FOR',I4,' CONSTANT-CONCENTRATION 
     &BOUNDARY CELLS')
  150 FORMAT(/'   LAYER     ROW  COLUMN      CONCENTRATION')
C
      RETURN
      END       
